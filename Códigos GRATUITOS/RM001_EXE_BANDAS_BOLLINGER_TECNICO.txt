// ###################################################################
// ######### FABRICA DE SETUPS - www.FabricaDeSetups.com.br ##########
// ###################################################################

// IMPORTANTE: Todos os códigos publicados pela Fábrica de Setup possuem
// carater educacional. Recomenda-se a sua utilização apenas em ambiente
// de simulação (Backtest). Qualquer aplicação em contas reais é de inteira
// responsabilidade do usuário, devendo o mesmo ter realizado todos os testes
// necessários para certificar de que o código se comporta da maneira que espera. 
// O software Profit Pro da Nelogica recebe constantemente atualizações que
// podem afetar o funcionamento de códigos já existentes. Desta forma, A Fábrica 
// de setup não se responsabiliza por nenhuma execução dos códigos em conta real.

// -------------------------------------------------------------------
//       CÓDIGO DO SETUP: VL001 - Bandas de Bollinger [Fechou fora, fechou dentro]
// DATA DE IMPLEMENTAÇÃO: 25/05/2023
//                VERSÃO: V1.0 (GRATUITA)
//    TIPO DE ESTRATÉGIA: EXECUÇÃO
//             DESCRIÇÃO: Sinaliza em verde o candle no qual haveria uma 
//                        compra e de vermelho quando haveria uma venda, 
//                        uma vez satisfeitas as condições estipuladas no
//                        setup de Bandas de Bollinger em sua versão mais básica
//
// -------------------------------------------------------------------

const 
// ------------------------ CONSTANTES -------------------------------
  cCorGatilhoCompra = clGreen;
  cCorGatilhoVenda  = clRed;
  cOffset = 10;
// ------------------------ CONSTANTES -------------------------------

input
// ------------------------ PARAMETROS -------------------------------
  pQtdePeriodos(20);
  pQtdeDesvios(2.0);
  pFatorRisco(1.6);
// ------------------------ PARAMETROS -------------------------------

var
// ------------------------- VARIAVEIS -------------------------------
 fMediaCentral, fBandaSuperior, fBandaInferior: float;
 fStoploss, fStoplossOffset, fTarget: float;
// ------------------------- VARIAVEIS -------------------------------

begin

// ---------------------------- SETUP --------------------------------
  fBandaSuperior := BollingerBands(pQtdeDesvios,pQtdePeriodos,0)|0|;
  fBandaInferior := BollingerBands(pQtdeDesvios,pQtdePeriodos,0)|1|;
  fMediaCentral := Media(pQtdePeriodos,TypicalPrice);

  plot(fMediaCentral);
  plot2(fBandaSuperior);
  plot3(fBandaInferior);

  SetPlotColor(1,clRed);
  SetPlotColor(2, clWhite);     
  SetPlotColor(3, clWhite);
  SetPlotStyle(2,psDash);
  SetPlotStyle(3,psDash);

  // Fechou fora, fechou dentro - Compra
  if  (Close > fBandaInferior)
  and (Close[1] < fBandaInferior)
  and (Close > Open)
  and not hasPosition
  then 
  begin
    PaintBar(clGreen);
    BuyLimit(High);
    fStoploss := Low;
    fStoplossOffset := fStoploss - cOffset*MinPriceIncrement;
    fTarget := High + pFatorRisco*Range;
  end;

  // Fechou fora, fechou dentro - Venda
  if  (Close < fBandaSuperior)
  and (Close[1] > fBandaSuperior)
  and (Close < Open)
  and not hasPosition
  then
  begin
    PaintBar(clRed);
    SellShortLimit(Low);
    fStoploss := High;
    fStoplossOffset := fStoploss + cOffset*MinPriceIncrement;
    fTarget := Low - pFatorRisco*Range;
  end;

  if isBought then
  begin
    SellToCoverLimit(fTarget);  
    SellToCoverStop(fStoploss, fStoplossOffset); 
    if (Close < fStoplossOffset) then ClosePosition;
  end;

  if isSold then 
  begin
    BuyToCoverLimit(fTarget);  
    BuyToCoverStop(fStoploss, fStoplossOffset); 
    if (Close > fStoplossOffset) then ClosePosition;
  end;


// ---------------------------- SETUP --------------------------------

end.